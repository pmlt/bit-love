press'a:
        ldx page
        cpx #num_pages
        bne +
        ; SHE SAID YES!!
        inc lock'input
        ; Draw last screen
        lda #<bgyes
        sta current'nt'ptr
        lda #>bgyes
        sta current'nt'ptr+1
        jsr disable'graphics
        jsr send'prose'page
        jsr enable'graphics
        ; Start music
*       rts

press'b:
        ldx page
        cpx #num_pages
        bne ++
        ; SHE SAID NO!!
        inc lock'input
        ; Make the NES meltdown
        ; Pipe CODE into PPU nametables!!
        lda #<reset
        sta current'nt'ptr
        lda #>reset
        sta current'nt'ptr+1
        inc meltdown
*       rts

press'left:
        jsr previous'page
        rts

press'right:
        jsr next'page
        rts

play'noise:
        pha
        lda #%00000101
        sta $400E
        lda #%00110011
        sta $400C
        pla
        rts

disable'graphics:
        lda #$00
        sta $2000
        sta $2001
        rts

enable'graphics:
        ; Set basic PPU registers.  Load background from $0000,
        ; sprites from $1000, and the name table from $2000.
        lda #%10001000
        sta $2000
        lda #%00011110
        sta $2001
        rts

previous'page:
        ldx page
        cpx #1
        beq +
        lda current'nt'ptr+1
        sbc #8
        sta current'nt'ptr+1
        jsr disable'graphics
        jsr send'prose'page
        jsr enable'graphics
        dec page
*       rts

next'page:
        ldx page
        cpx #num_pages
        beq +
        jsr disable'graphics
        jsr send'prose'page
        jsr enable'graphics
        inc page
*       rts

; Blast fade level
send'fade'level:
        ;Blast correct fade level
        lda #$3F
        ldy #$02
        sta $2006
        sty $2006
        lda bg'fade'level,x
        sta $2007
        lda fg'fade'level,x
        sta $2007
        rts

; Blast the two pages of prose pointed to to VRAM
send'prose'page:
        lda #$24
        sta $2006
        lda #$00
        sta $2006
        ; Send bg
        ldy #$00
        ldx #$04
*       lda (current'nt'ptr),y
        sta $2007
        iny
        bne -
        inc current'nt'ptr+1
        dex
        bne -
        rts

scroll'up:
        ldx scroll
        beq +
        dex
        stx scroll
*       rts

scroll'down:
       ldx scroll
       cpx #239
       beq +
       inx
       stx scroll
*      rts

send'scroll:
        ldx #$00   ; Reset VRAM
        stx $2006
        stx $2006
        
        lda #$00
        ldx scroll
        sta $2005  ; Write 0 for horizontal scroll value
        stx $2005  ; Write scroll value for Vert. Scroll value
        rts
