; Program code for NES 101 Tutorial
; Code by Michael Martin, 2001-2

; Assign the sprite page to page 2.
.alias        sprite        $200

; Allocate memory in the zero page segment.  If we
; really wanted to, we could scatter these declarations
; through the code (P65 1.1 lets us do so) but not all
; assemblers allow this, and it doesn't help clarity
; any on this program.  So the heck with it.

; As a side note, P65 doesn't really grant any special
; status to any of the segments you use, and only has
; "text" and "data" built in.  This means that "zp"
; could be named whatever we wanted, and it also means
; that we have to tell it where to start from (it's the
; zero page, so we start it from zero, naturally).

.data zp
.org $0000
.space coord'x 1
.space coord'y 1
.space column 1
.space line 1
.space sprite'ptr 2

.space prose'ptr 2
.space scroll 1

; If we had a normal data segment, it would have an .org $0300, so
; that it doesn't stomp on our sprite data.

; Actual program code.  We only have one PRG-ROM chip here, so the
; origin is $C000.
.text
.org $C000

reset:  sei
        cld
        ; Wait two VBLANKs.
*       lda $2002
        bpl -
*       lda $2002
        bpl -

        ; Clear out RAM.
        lda #$00
        ldx #$00
*       sta $000,x
        sta $100,x
        sta $200,x
        sta $300,x
        sta $400,x
        sta $500,x
        sta $600,x
        sta $700,x
        inx
        bne -

        ; Reset the stack pointer.
        ldx #$FF
        txs

        ; Disable all graphics.
        lda #$00
        sta $2000
        sta $2001

        jsr init'graphics
        jsr init'input
        jsr init'sound

        ; Set basic PPU registers.  Load background from $0000,
        ; sprites from $1000, and the name table from $2000.
        lda #%10001000
        sta $2000
        lda #%00011110
        sta $2001

        cli

        ; Transfer control to the VBLANK routines.
loop:   jmp loop

init'graphics:
        jsr init'sprites
        jsr load'palette
        jsr load'name'tables
        jsr init'scrolling
        rts

init'input:
        rts

init'sound:
        ; initialize sound hardware
        lda #$01
        sta $4015
        lda #$00
        sta $4001
        lda #$40
        sta $4017
        rts

init'sprites:
        rts

init'scrolling:
        lda #0
        sta scroll
        rts

; Load palette into $3F00
load'palette:
        lda #$3F
        ldx #$00
        sta $2006
        stx $2006
*       lda palette,x
        sta $2007
        inx
        cpx #$20
        bne -
        rts

load'name'tables:
; Jam some text into the first name table (at $2400, thanks to mirroring)
        lda #<bg
        sta prose'ptr
        lda #>bg
        sta prose'ptr+1
        lda #$24
        sta $2006
        lda #$00
        sta $2006
        jsr send'prose'page

; Clear out the Name Table at $2800 (where we already are.  Yay.)
        jsr send'prose'page
        rts

update'sprite:
        lda #>sprite
        sta $4014                ; Jam page $200-$2FF into SPR-RAM
        rts

react'to'input:
        lda #$01        ; strobe joypad
        sta $4016
        lda #$00
        sta $4016

        lda $4016        ; A does nothing
        lda $4016        ; B does nothing
        lda $4016        ; Select does nothing
        lda $4016        ; Start does nothing
        lda $4016        ; Up
        and #1
        beq +
        jsr scroll'up
*       lda $4016        ; Down
        and #1
        beq +
        jsr scroll'down
*       rts              ; Ignore left and right, we don't use 'em

vblank: 
        jsr update'sprite
        jsr react'to'input
        jsr send'scroll
irq:    rti

.include "lib.oph"

.include "bg.oph"

.advance $FFFA
.word vblank, reset, irq
